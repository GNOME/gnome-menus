AC_INIT(libmenu/menu-tree.h)

AM_INIT_AUTOMAKE(gnome-menus, 2.9.5)
AM_CONFIG_HEADER(config.h)

# Before making a release, the LT_VERSION string should be modified.
# The string is of the form C:R:A.
# - If interfaces have been changed or added, but binary compatibility has
#   been preserved, change to C+1:0:A+1
# - If binary compatibility has been broken (eg removed or changed interfaces)
#   change to C+1:0:0
# - If the interface is the same as the previous version, change to C:R+1:A

LIB_MENU_LT_VERSION=0:0:0
AC_SUBST(LIB_MENU_LT_VERSION)

AM_MAINTAINER_MODE

GETTEXT_PACKAGE=gnome-menus
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Name of default gettext domain])
AC_SUBST(GETTEXT_PACKAGE)

ALL_LINGUAS="bg cs da de el en_CA es et eu fr hu ja lt ms nb nl no pa pt_BR pt ru sk sr sr@Latn sv th uk zh_CN"
AM_GLIB_GNU_GETTEXT

AC_PROG_INTLTOOL([0.31])

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AC_ARG_PROGRAM
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

dnl make sure we keep ACLOCAL_FLAGS around for maintainer builds to work
AC_SUBST(ACLOCAL_AMFLAGS, "$ACLOCAL_FLAGS")

PKG_CHECK_MODULES(LIBMENU,
                  glib-2.0 >= 2.5.6 \
		  gnome-vfs-2.0 >= 2.8.2)
AC_SUBST(LIBMENU_CFLAGS)
AC_SUBST(LIBMENU_LIBS)

GNOME_COMPILE_WARNINGS(yes)

AC_ARG_ENABLE(deprecations,
              [AC_HELP_STRING([--enable-deprecations],
                              [warn about deprecated usages [default=no]])],,
              [enable_deprecations=no])

if test "x$enable_deprecations" = "xyes"; then
   DISABLE_DEPRECATED_CFLAGS="\
-DG_DISABLE_DEPRECATED \
-DGDK_DISABLE_DEPRECATED \
-DGTK_DISABLE_DEPRECATED \
-DGDK_PIXBUF_DISABLE_DEPRECATED \
-DGNOME_DISABLE_DEPRECATED"
   AC_SUBST(DISABLE_DEPRECATED_CFLAGS)
fi

dnl --enable-debug=(yes|minimum|no)
AC_ARG_ENABLE(debug, [  --enable-debug=[no/yes] turn on debugging [default=no]],,enable_debug=yes)
if test "$enable_debug" = "yes"; then
  DEBUG_CFLAGS="-DG_ENABLE_DEBUG"
else
  if test "x$enable_debug" = "xno"; then
    DEBUG_CFLAGS="-DG_DISABLE_ASSERT -DG_DISABLE_CHECKS"
  else
    DEBUG_CFLAGS=""
  fi
fi
AC_SUBST(DEBUG_CFLAGS)

AC_OUTPUT([
Makefile
libmenu/Makefile
libmenu/libgnome-menu.pc
layout/Makefile
desktop-directories/Makefile
util/Makefile
po/Makefile.in
])
